# Author: Jelte van Waterschoot
# Group: Human Media Interaction, University of Twente
# Contact: j.b.vanwaterschoot@utwente.nl

? SET {'title':'Babbelbot'}
#Profile of the user we want to save to firebase
# ? userdata = FETCH name, birthyear, birthplace, childhoodplace, old_games, new_games

? IF bliss_data_sd:profile = NULL
    ? INSERT BLOCK:bliss_data_sd:resetData
    ? INSERT BLOCK:bliss_shared:resetData
    ? INSERT BLOCK:bliss_secrets:resetData

? topics = [{"id":7327,"name":"Gezelschapsspelen, kaart- en bordspelen overig"},{"id":7310,"name":"Individuele spelletjes, ongespecificeerd"},{"id":7331,"name":"Computer spelen, games offline"},{"id":3430,"name":"Huisdieren verzorgen"},{"id":4000,"name":"Vrijwilligerswerk"},{"id":5120,"name":"Op visite gaan en visite krijgen, ongespecificeerd"}]
? pets = ["hond","kat","egel","geit","toer","steenbok","muis","gerbil","renmuis","grasmuis","kangoeroe","wallabie","konijn","rat", "cavia","hamster","nerts","paard","ezel","varken","rund","waterbuffel","damhert","edelhert","schaap","vis","vogel","duif","poes","fret","chinchilla","parkiet","papegaai","kaketoe","ara","valk","roofvogel","slang","schildpad","salamander","leguaan","eend","gans"]
? family = ["broer","zus","pa","pap","vader","papa","pappa","mam","ma","mamma","mama","moeder","ouder","kind","neef","nicht","kleinkind","achterkleinkind","oom","tante","achterneef","achternicht","opa","grootvader","oma","grootmoeder","zwager","zwageres"]

# CONFIGURATION
# Reset?
# ? INSERT BLOCK:bliss_data_sd:resetData
# Record audio?
? bliss_shared:record = 1
# Speak with emphasis?
#? bliss_shared:emphasis = 1
# Show user transcript?
#? bliss_shared:user_speech = 1
# Show agent transcript?
#? bliss_shared:agent_speech = 1

#######################################################
# START OF DIALOGUE
#######################################################

? bliss_shared:text = 'Dit is een demo van het BLISS project, waarbij je een gesprek gaat voeren met een gesproken agent over geluk. We hebben een <a href="https://drive.google.com/file/d/1p2tc28B8GfqOsXdNV6KuEDpU7-i5qClp/view" target="_blank">informatiebrochure</a> die verder in gaat op de details van deze demo die ook voorbeelden bevat van het soort vragen dat je krijgt. Wil je alsjeblieft het <a href="https://forms.office.com/r/tqg5f58yjC" target="_blank">toestemmingsformulier</a> invullen voordat je verder gaat?'
<<bliss_shared:text>>
# ? info = ASK URL {'url': 'https://forms.office.com/r/tqg5f58yjC', 'target':'_blank'}

? bliss_shared:text = "Heb je toestemming gegeven op het formulier?"
<<bliss_shared:text>>
? ASK ANSWER
    > Ja
        ? consent = 1
        ? bliss_shared:text = "Dankjewel. Dan starten we nu de conversatie van de demo. Zorg dat je voor de antwoorden waarvoor je de microfoon gebruikt, eerst drukt op het microfoontje en om de opname te stoppen het groene vinkje/blokje (NIET de rode knop/het rode kruis)."
    > Nee
        ? consent = 0
        ? bliss_shared:text = "Helaas, dan kun je niet meedoen aan de demo."
<<bliss_shared:text>>


? IF consent = 1

    ? SET {'delay': 2000}  
    # Check if they want another go if all questions are asked
    ? IF LENGTH bliss_data_sd:profile["ids"] = LENGTH topics
        ? bliss_shared:text = "We hebben al een sessie voltooid! Wil je opnieuw starten?"
        <<bliss_shared:text>>
        ? ASK ANSWER
            > Ja
                ? INSERT BLOCK:bliss_data_sd:resetData
                ? bliss_shared:text = "Ok! Dan gaan we verder met de vragen."
                <<bliss_shared:text>>
            > Nee
                ? bliss_shared:text = "Geen probleem."
                <<bliss_shared:text>>        

    # Introduction
    ? IF bliss_data_sd:profile['name'] != NULL
        ? bliss_shared:text = "Welkom terug, " +bliss_data_sd:profile['name']    
        ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
        ? bliss_shared:text = "Wil je nog een gesprek met mij voeren?"
        ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
        ? ASK ANSWER
            > Ja
                ? intent = "affirm"            
                ? INSERT BLOCK:bliss_data_sd:resetData
            > Nee
                ? intent = "deny"            
        ? bliss_data_sd:profile['log'] := {'other': {'intent':intent}}
    ? ELSE
        ? bliss_shared:text = "Hallo, ik ben Babbelbot en ik zou graag even met je willen praten."
        ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis

    ? bliss_shared:text = "Het is de bedoeling dat ik meer leer over het geluk en welzijn van mensen. Ik zou daarom graag met je praten over waar je je goed bij voelt."
    ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis

    # Opening question
    ? INSERT BLOCK:openEvent
    
    # Loop through all topic sequences in random order
    ? WHILE LENGTH bliss_data_sd:profile["ids"] < LENGTH topics
        ? INSERT BLOCK:selectTopic
        ? IF topic["id"] = 3430
            ? INSERT BLOCK:3430
        ? IF topic["id"] = 4000
            ? INSERT BLOCK:4000
        ? IF topic["id"] = 5120
            ? INSERT BLOCK:5120
        ? IF topic["id"] = 7310
            ? INSERT BLOCK:7310
        ? IF topic["id"] = 7327
            ? INSERT BLOCK:7327
        ? IF topic["id"] = 7331
            ? INSERT BLOCK:7331

    # Wrapping up the dialogue
    ? bliss_shared:text = "Dat was het voor deze keer! Van harte bedankt! Wat vond je van het gesprek?"
    ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis                        
    ? ASK ANSWER
        > Wel oké
            ? intent = "doubt"
            ? bliss_shared:text = "Ik zal nog beter m'n best doen om het leuker te maken! Tot ziens!"
        > Heel leuk
            ? intent = "affirm"
            ? bliss_shared:text = "Leuk, tot de volgende keer!"
        > Niet heel interessant
            ? intent = "deny"
            ? bliss_shared:text = "Oei, jammer... Ik hoop dat je me nog een nieuwe kans geeft in de toekomst. Tot snel!"
        > Slecht
            ? intent = "deny"
            ? bliss_shared:text = "Oei, jammer... Ik hoop dat je me nog een nieuwe kans geeft in de toekomst. Tot snel!"
    ? bliss_data_sd:profile['log'] := {'other': {'intent':intent}}
    ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis

#######################################################
# END OF DIALOGUE
#######################################################

# State a random opening question
? BLOCK openEvent
    ? _open_questions = ["Heb je deze week iets leuks meegemaakt?","Heb je afgelopen tijd iets nieuws gedaan?","Heb je wat nieuwtjes te vertellen?"]
    ? _open_question = _open_questions[NOW % LENGTH _open_questions]
    ? bliss_shared:text = _open_question
    ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
    ? INSERT BLOCK:bliss_shared:getASRTranscript 
    ? bliss_shared:text = bliss_shared:reply['hypotheses'][0]['utterance']
    ? INSERT BLOCK:bliss_shared:numberOfWords                          
    ? _number_of_words = bliss_shared:reply['words']
    ? IF _number_of_words > 3
        ? bliss_shared:text = "Dat is interessant!"
    ? ELSE
        ? bliss_shared:text = "Oké."
    ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
                             

# Bron: Alle ballen in de lucht, Tijdsbesteding in Nederland en de samenhang met kwaliteit van leven.
# Auteur: Anne Roeters (redactie).
# Sociaal en Cultureel Planbureau
# December 2018
# ISBN/ISSN/anders: 978 90 377 0899 8.
# Volgnummer 2018-34.

# Select a topic randomly, but not previously from the list of the CBS vrijetijdsbesteding. Reset if max items has been reached.
? BLOCK selectTopic
    # ? IF LENGTH bliss_data_sd:profile["ids"] >= LENGTH topics
    #     ? bliss_data_sd:profile["ids"] = []    
    ? topic = topics[NOW % LENGTH topics]
    ? _id = topic["id"]
    ? _existing_topic = bliss_data_sd:profile["ids"] FIND THIS = _id
    ? IF _existing_topic != NULL
        ? WHILE _existing_topic != NULL
            ? topic = topics[NOW % LENGTH topics]
            ? _id = topic["id"]
            ? _existing_topic = bliss_data_sd:profile["ids"] FIND THIS = _id        
    ? bliss_data_sd:profile["ids"] = FLAT [bliss_data_sd:profile["ids"],_id]
    # ? ids = STRINGIFY bliss_data_sd:profile["ids"]
    # ? text = "IDS in database: " + ids
    # <<text>>

? BLOCK checkPets
    ? _indexNPS = 0
    ? noKnownPet = 1
    ? pet = "dat"
    ? WHILE _indexNPS < LENGTH nps AND noKnownPet = 1
        ? noKnownPet = pets FIND THIS = nps[_indexNPS]
        ? IF noKnownPet != NULL
            ? pet = nps[_indexNPS]
            ? noKnownPet = 0
        ? _indexNPS = _indexNPS + 1

# 3430 sequence
? BLOCK 3430
    ? _sd = NOW % 2
    ? IF _sd = 0
        ? bliss_shared:text = "Heb je huisdieren?"
        ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
        ? ASK ANSWER
            > Ja / Gehad
                ? intent = "affirm"
                ? bliss_shared:text = "Wat vind je het leukste aan een huisdier?"            
            > Nee 
                ? intent = "deny"
                ? bliss_shared:text = "Zou je overwegen om in de toekomst een huisdier te hebben?"            
        ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
        ? INSERT BLOCK:bliss_shared:getASRTranscript                                                    
    ? ELSE
        ? bliss_shared:text = "Als Babbelbot praat ik niet alleen met mensen, maar ik lees regelmatig de online krantenberichten om een beetje bij te blijven. Zo las ik laatst een bericht dat dankzij de coronacrisis de kip superpopulair is als huisdier! Heb je huisdieren?"
        ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
        ? ASK ANSWER
            > Ja / Gehad
                ? intent = "affirm"
                ? bliss_shared:text = "Welk huisdier?"
                ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
                ? INSERT BLOCK:bliss_shared:getASRTranscript
                ? bliss_shared:text = bliss_shared:reply['hypotheses'][0]['utterance']
                ? INSERT BLOCK:bliss_shared:getNPS
                ? nps = bliss_shared:reply["chunks"]
                ? INSERT BLOCK:checkPets
                
                ? IF noKnownPet = 0
                    ? bliss_shared:text = "Waarom is het zo leuk om een " +pet+ " als huisdier te hebben?"            
                ? ELSE
                    ? bliss_shared:text = "Vertel daar eens iets meer over."                     
            > Nee
                ? intent = "deny"
                ? bliss_shared:text = "Zou je je kunnen voorstellen dat je een kip als huisdier zou hebben?"
        ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
        ? INSERT BLOCK:bliss_shared:getASRTranscript       

# 4000 sequence
? BLOCK 4000
    ? _sd = NOW % 2
    ? IF _sd = 0
        ? bliss_shared:text = "Doe je ook vrijwilligerswerk of help je wel eens iemand?"
        ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
        ? INSERT BLOCK:bliss_shared:getASRTranscript
    ? ELSE
        ? bliss_shared:text = "Ik zou even met je willen praten over het doen van vrijwilligerswerk. Dan denk je misschien al meteen aan helpen in de soepkeuken van het Leger des Heils. Maar ook juist kleine dingen zoals een keer een boodschap meenemen voor de buurvrouw of de afvalcontainers aan straat zetten tellen ook mee. Wie help je wel eens?"
        ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
        ? INSERT BLOCK:bliss_shared:getASRTranscript
        
        # Check if not dog or home is mentioned already
        ? bliss_shared:text = bliss_shared:reply['hypotheses'][0]['utterance']
        ? INSERT BLOCK:bliss_shared:getNPS
        ? IF LENGTH bliss_shared:reply["chunks"] = 0
            ? bliss_shared:text = "Laat je wel eens de hond uit voor iemand of pas je wel eens op het huis als mensen op vakantie gaan?"
        ? ELSE
            ? _dog = bliss_shared:reply["chunks"] FIND THIS = "hond"
            ? _home = bliss_shared:reply["chunks"] FIND THIS = "huis"
            ? IF _dog = NULL AND _home = NULL            
                ? bliss_shared:text = "Laat je wel eens de hond uit voor iemand of pas je wel eens op het huis als mensen op vakantie gaan?"
        ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
        ? INSERT BLOCK:bliss_shared:getASRTranscript                                                        
    ? bliss_shared:text = "Ben je een beetje handig in klussen binnen of buitenshuis?"
    ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
    ? ASK ANSWER
        > Ja
            ? intent = "affirm"
        > Nee
            ? intent = "deny"
    ? IF intent = "affirm"
        ? bliss_shared:text = "Doe je dat wel eens voor iemand anders?"
    ? ELSE
        ? bliss_shared:text = "Ben je wel handig met computers.."
    ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
    ? INSERT BLOCK:bliss_shared:getASRTranscript                                                    

# 5120 
? BLOCK 5120
    ? _sd = NOW % 2
    ? IF _sd = 0
        ? _5120_varations = ["Bij wie ben je recent op bezoek geweest?","Dan even over op visite gaan, naar wie ben je van plan binnenkort op bezoek te gaan?","Krijg je binnenkort visite langs?"]
        ? bliss_shared:text = _5120_varations[NOW % LENGTH _5120_varations]
    ? ELSE
        ? bliss_shared:text = "Eindelijk hoeven mensen nu niet meer altijd een mondkapje op en mogen mensen weer gewoon bij elkaar op bezoek. Daar kijken mensen al een hele tijd weer naar uit. Bij wie ben je recent op bezoek geweest?"
    ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
    ? INSERT BLOCK:bliss_shared:getASRTranscript                            

# 7310 sequence
? BLOCK 7310
    ? _sd = NOW % 2
    ? IF _sd = 0
        ? bliss_shared:text = "Speel je zelf vaak alleen een spelletje zoals patience, het maken van een puzzel of het doen van kruiswoordpuzzels?"        
    ? ELSE
        ? bliss_shared:text = "Als computer speel ik graag spelletjes tegen mezelf, mijn grote voorbeeld is mijn voorganger DeepBlue. Je weet wel, het schaakprogramma dat won van schaakgrootmeester Kasparov. Speel je zelf vaak alleen een spelletje zoals patience, het maken van een puzzel of het doen van kruiswoordpuzzels?"
    ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
    ? INSERT BLOCK:bliss_shared:getASRTranscript                            

# 7327 sequence
? BLOCK 7327
    ? _sd = NOW % 2
    ? IF _sd = 0
        ? bliss_shared:text = "Vind je het leuk om gezelschapsspelletjes te spelen?"        
    ? ELSE
        ? bliss_shared:text = "Laatst sprak ik met iemand die vertelde dat ze graag WordFeud speelt, dat is een soort digitale variant van Scrabble. Speel je dat ook wel eens?"
    ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
    ? ASK ANSWER
        > Ja           
            ? intent = "affirm"            
        > Nee
            ? intent = "deny"  
    ? IF _sd = 1
        ? IF intent = "affirm"
            ? bliss_shared:text = "Met wie speel je dan vaak WordFeud?"
            ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
            ? INSERT BLOCK:bliss_shared:getASRTranscript 
        ? bliss_shared:text = "Is er een ander gezelschapsspel dat je graag speelt?"        
        ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
        ? INSERT BLOCK:bliss_shared:getASRTranscript
    ? ELSE
        ? IF intent = "affirm"
            ? bliss_shared:text = "Welk gezelschapsspel speel je graag?"                    
            ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
            ? INSERT BLOCK:bliss_shared:getASRTranscript    
                                    

# 7331 sequence
? BLOCK 7331
    ? _sd = NOW % 2
    ? IF _sd = 0
        ? bliss_shared:text = "Speel je graag games op de computer?"
    ? ELSE
        ? bliss_shared:text = "Als ik met iemand spreek die onder de dertig is, dan krijg ik tegenwoordig heel vaak als antwoord dat ze graag op de computer gamen, meestal met vrienden. Vind je dat ook leuk om te doen?"
    ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
    ? INSERT BLOCK:bliss_shared:getASRTranscript                            

# VIP sequence
? BLOCK 0001
    ? bliss_shared:text = "Dan zou ik graag eens met je willen praten over mensen die belangrijk zijn in je leven."
    ? INSERT BLOCK:bliss_shared:speakTextWithEmphasis
    ? bliss_shared:text = "Als je dan aan je familie denkt, wie is dan de eerste die in je gedachte opkomt?"
    ? _sd = NOW % 2
    ? IF _sd = 0
        